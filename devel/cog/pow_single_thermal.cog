# Jedi Knight Cog Script
#
# POW_SINGLE_THERMAL.COG
#
# POWERUP Script - single Thermal Detonator
#
# [YB & CYW]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved


symbols

thing       powerup                          local
thing       player                           local
int         bin=4                            local
sound       pickupsnd=thrmlpu2.wav           local
sound       respawnsnd=Activate01.wav        local
flex        amount                           local

int         bin_contents=0                   local
int         autopickup=0                     local

template	expl_tpl=+grenade_exp			local
template	sphere_tpl=+TD_Expl_Sphere		local
sector		sec								local
vector		pos								local

message		created
message		pulse
message		damaged
message		timer
message     touched
message     taken
message     respawn

end

# ========================================================================================

code
	
created:
	parseArg(GetSenderRef(), "soundclass=det.snd");				// so it makes "ping" noises when it bounces
	if(!IsMulti())
		SetThingPulse(GetSenderRef(), 15);						// repeat in a pulse, so savegames will load the changes
	return;

pulse:
	parseArg(GetSenderRef(), "soundclass=det.snd");				// so it makes "ping" noises when it bounces
	return;

damaged:
	SetTimerEx(rand() * 0.4, GetSenderRef(), 0, 0);
	return;

timer:
	powerup = getSenderID();
	sec = GetThingSector(powerup);
	pos = GetThingPos(powerup);
	TakeItem(powerup, powerup);
	CreateThingAtPos(expl_tpl, sec, pos, '0 0 0');
	CreateThingAtPos(sphere_tpl, sec, pos, '0 0 0');
	return;

touched:
   player = GetSourceRef();
   amount = GetInv(player, bin);

   if (amount < GetInvMax(player, bin))
   {
      powerup = GetSenderRef();
      TakeItem(powerup, player);
   }

   Return;

# ........................................................................................

taken:
   player = GetSourceRef();
   powerup = GetSenderRef();
	killTimerEx(powerup);
   // Print("Thermal Detonator");
   jkPrintUNIString(player, 205);

   // Do effects.
   PlaySoundThing(pickupsnd, powerup, 1.0, -1, -1, 0);
   AddDynamicTint(player, 0.0, 0.0, 0.2);

   // store the old bin contents
   bin_contents = GetInv(player, bin);

   // Increment powerup amount.
   ChangeInv(player, bin, 1.0);

   // Check for Auto Pickup
   autopickup = GetAutoPickup();
   if((autopickup & 1) && !(autopickup & 2)) // DANGEROUS
   {
      if(!bin_contents)
      {
         if(!((autopickup & 4) && (GetCurWeapon(player) >= bin)))
         {
            if(!((autopickup & 8) && (GetCurWeapon(player) == 10)))
            {
               SelectWeapon(player, bin);
               Return;
            }
         }
      }
   }

   // Check for Auto Reload
   if(GetAutoReload() & 1)
   {
      if(!bin_contents)
      {
         if(!((GetAutoReload() & 2) && (GetCurWeapon(player) == 10)))
         {
            // Try to autoselect and see if the best weapon is the TD
            if(AutoSelectWeapon(player, 2) == 4) SelectWeapon(player, 4);
         }
      }
   }

   Return;

# ........................................................................................

respawn:
   powerup = GetSenderRef();
   PlaySoundThing(respawnsnd, powerup, 0.6, 5.0, 10.0, 0);
   	parseArg(GetSenderRef(), "soundclass=det.snd");				// so it makes "ping" noises when it bounces
   Return;

end





