# Jedi Knight Cog Script
#
# class_det.cog
#
# Class Cog - Thermal Detonators
#
# Makes live Thermal Detonator Projectiles tick
# Also makes timed detonators beep just before exploding.
#
# [darthslaw]
#
# 3/13/07 - Moved the code from the bouncy det cog into this one, so there is only 1 TD class cog
#
# (c) 2007 JK Recog

symbols

message		created
message		removed
message		timer

sound		warnSound=ThermWarn3.wav		local
sound		loopSound=ThermLoop02.wav		local

int			channel=-1						local

thing		dummy							local
thing		player							local
thing		TD								local

template	expl_sphere=+TD_Expl_Sphere		local

template	tpl								local
# impact grenades
template	tpl1=+grenade1					local
template	tpl2=+actor_grenade				local
# bouncy grenades
template	tpl3=+grenade2					local
template	tpl4=+actor_grenade2			local
template	tpl5=+spec_grenade				local

int			i								local

end

code

created:
	dummy = GetSenderRef();
	tpl = GetThingTemplate(dummy);
	if(tpl == tpl1 || tpl == tpl2)
	{
		// if impact grenade, just play the ticking sound and return
		PlaySoundThing(loopSound, dummy, 1.0, -1, -1, 0x81);
	}
	else
	if(tpl == tpl3 || tpl == tpl4 || tpl == tpl5)
	{
		// if bouncy grenade, play ticking sound, and set timers to speed it up and play warning beep
		channel = PlaySoundThing(loopSound, dummy, 1.0, -1, -1, 0x81);
		if(channel != -1)
			SetTimerEx(2.0, dummy + 10, 1, channel);	// timer to increase ticking rate
		SetTimerEx(2.4, dummy + 10, 2, dummy);			// timer to make warning beep
	}
	Return;
	
removed:
	TD = GetSenderRef();
	tpl = GetThingTemplate(TD);
	if(tpl == tpl1 || tpl == tpl2 || tpl == tpl3 || tpl == tpl4 || tpl == tpl5)
	{
		KillTimerEx(10 + TD);		// no more timers are needed for this TD
		// do the light flash effects
		for(i=0;i<=GetNumPlayers();i=i+1)
		{
			player = GetPlayerThing(i);
			if(HasLOS(player, TD))
			{
				// move this code to kyle.cog / skill message... need to check local players' bins for IR Goggles usage
				SkillTarget(player, TD, 41, 200);
			}
		}
		// move to a trigger
		FireProjectile(TD, expl_sphere, -1, -1, '0 0 0', '0 0 0', 0, 0, 0, 0);
	}
	return;

timer:
	if(GetParam(0) == 1)
	{
		ChangeSoundPitch(GetParam(1), 1.5, 0.5);						//beep faster...
	}
	else if(GetParam(0) == 2)
	{
		PlaySoundThing(WarnSound, GetParam(1), 1.0, -1, -1, 0x80);		//WARNING!!!
	}
	return;

end
