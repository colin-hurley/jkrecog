# Jedi Knight Cog Script
#
# POW_RAILCHARGES.COG
#
# POWERUP Script - Rail Charges
#
# [YB & CYW]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved


symbols

thing       powerup                          local
thing       player                           local
int         bin=15                           local
sound       pickupsnd=helthpu1.wav           local
sound       respawnsnd=Activate01.wav        local
flex        amount                           local

int         bin_contents=0                   local

template	expl_tpl=+raildet_exp			local
sector		sec								local
vector		pos								local

message		damaged
message		timer
message     touched
message     taken
message     respawn

end

# ========================================================================================

code

damaged:
	SetTimerEx(rand() * 0.4, GetSenderRef(), 0, 0);
	return;

timer:
	powerup = getSenderID();
	sec = GetThingSector(powerup);
	pos = GetThingPos(powerup);
	TakeItem(powerup, powerup);
	CreateThingAtPos(expl_tpl, sec, pos, '0 0 0');
	return;

touched:
   player = GetSourceRef();
   amount = GetInv(player, bin);

   if (amount < GetInvMax(player, bin))
   {
      powerup = GetSenderRef();
      TakeItem(powerup, player);
   }

   Return;

# ........................................................................................

taken:
   player = GetSourceRef();
   powerup = GetSenderRef();
	killTimerEx(powerup);
   // Print("Rail Charges");
   jkPrintUNIString(player, bin);

   // Do effects.
   PlaySoundThing(pickupsnd, powerup, 1.0, -1, -1, 0);
   AddDynamicTint(player, 0.0, 0.0, 0.2);

   // store the old bin contents
   bin_contents = GetInv(player, bin);

   // Increment powerup amount.
   ChangeInv(player, bin, 3.0);

   // Check for Auto Reload
   if(GetAutoReload() & 1)
   {
      if(!bin_contents)
      {
         if(!((GetAutoReload() & 2) && (GetCurWeapon(player) == 10)))
         {
            // Try to autoselect and see if the best weapon is the railgun
            if(AutoSelectWeapon(player, 2) == 7) SelectWeapon(player, 7);
         }
      }
   }

   Return;

# ........................................................................................

respawn:
   powerup = GetSenderRef();
   PlaySoundThing(respawnsnd, powerup, 0.6, 5.0, 10.0, 0);

   Return;

end


