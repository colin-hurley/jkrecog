# Jedi Knight Cog Script
#
# FORCE_GRIP.COG
#
# FORCEPOWER Script - The Grip
#  Dark Side Power
#  Bin 31
#
# This power will choke the life from its victims.
#
# The victim will be lifted from the ground and immobilized while gripped.
#
# Point where you want the victim to go and they will drift there.
#
# [darthslaw]
#
# (C) 2007 JK ReCog
# =======================================================================
symbols

thing       player                           local
thing       victim                           local
thing       potential                        local

flex        cost=5.0                         local
flex        mana                             local
flex        dot                              local
flex        maxDot                           local
flex        damage                           local
flex        starthealth                      local
int         rank                             local
#int         retval=0                         local

sound       gripSound=ForceGrip01.WAV        local

#int         limit                            local
int         count                            local

int         active=0                         local
int         targetting=0                     local

thing		dummy							local
template	ghost_tpl=+grip_ghost			local
vector		vel								local

int			old_flags=0						local

int			gripTrack=-1					local
int			chokeTrack=-1					local

message     startup
message     activated
message     deactivated
message     pulse
message     timer
message     newplayer
message     killed
message     selected
message     deselected

end
# =======================================================================
code
# .......................................................................
startup:
	player = GetLocalPlayerThing();
	SetInvActivated(player, 31, 0);
	victim = -1;
	active = 0;
	gripTrack = -1;
	chokeTrack = -1;
	Return;
# .......................................................................
activated:
	// Cannot use power if blinded
	if(active) return;
	if(GetActorFlags(player) & 0x800) Return;
	mana = GetInv(player, 14);
	if(mana >= cost)
	{
		rank = GetInv(player, 31);
		starthealth = GetThingHealth(player);
		victim = -1;
		SetInvActivated(player, 31, 1);
		targetting = 1;
		SetPulse(0.1);
		active = 1;
	}
	Return;
# .......................................................................
deactivated:
	if(!active) return;
	call stop_power;
	SetBinWait(player, 31, 1.0);
	Return;
# .......................................................................
pulse:


		## camera code
	#	angle = angle % 360;
	#	angle = angle + 5;
	#	dummy = FireProjectile(player, ghost_tpl, -1, -1, '0.15 -0.1 0', '0 0 0', 0, 0, 0, 0x0);
	#	SetLifeLeft(dummy, 0.3);
	#	vecmid = VectorScale(vectoradd(GetThingPos(victim),GetThingPos(player)), 0.5);
	#	SetThingLook(dummy, VectorSub(vecmid, GetThingPos(dummy)));
	#	SetCameraFocus(0, dummy);
	#	SetCurrentCamera(0);
		
		

	if(
		GetThingHealth(player) <= 0					//player dead
		|| GetInv(player, 14) < cost				//not enough mana
		|| GetThingHealth(player) < starthealth		//physical attack
	)
	{
		call stop_power;
		return;
	}
	
	if(targetting == 1)
	{
		// Check all things for our victim.
		victim = -1;
		maxDot = 0;
		// Search for all players and actors.
		potential = FirstThingInView(player, 70, 4, 0x404);
		while(potential != -1)
		{
			if(
				HasLOS(player, potential)
				&& (potential != player)
				&& (VectorDist(GetThingPos(player), GetThingPos(potential)) <= 1)
				&& !(GetThingFlags(potential) & 0x200)
				&& !(GetActorFlags(potential) & 0x100)
				&& !((jkGetFlags(potential) & 0x20) && !IsInvActivated(player, 23))
			)
			{
				if(!BitTest(GetActorFlags(potential), 0x100))
				{
					dot = ThingViewDot(player, potential);
					if(dot > maxDot)
					{
						victim = potential;
						maxDot = dot;
					}
				}
			}
			potential = NextThingInView();
		}
		// If we have a victim...
		if(victim != -1)
		{
			damage = SkillTarget(victim, player, 31, 0);
			old_flags = GetPhysicsFlags(victim);		// remember what the original flags were
			if(damage <= 0.0) return;
			SetInvActivated(player, 31, 1);
			// choke animation
			chokeTrack = PlayMode(victim, 25);
			// grip animation
			gripTrack = PlayMode(player, 36);
			targetting = 0;
		}
	}
	if(targetting == 0)
	{
		if(
			(victim == -1)													//no target
			|| (GetThingHealth(victim) <= 0)								//target dead
			|| (GetThingFlags(victim) & 0x200)								//target dead
			|| (!HasLOS(player, victim))									//we've lost LOS
			|| (VectorDist(GetThingPos(player), GetThingPos(victim)) > 1)	// out of range
		)
		{
			call stop_power;
			return;
		}
		
		if(GetInv(player, 64) != 1)
			ChangeInv(player, 14, -cost);
		
		dummy = FireProjectile(player, ghost_tpl, -1, -1, '0 0.4 0', '0 0 0', 0, 0, 0, 0x0);
		
		vel = VectorScale(VectorSub(GetThingPos(dummy), GetThingPos(victim)), 4.5);
		ClearPhysicsFlags(victim, 0x43);
		DetachThing(victim);
		if(VectorLen(vel) > 0.04)
		{
			SetThingVel(victim, vel);
		}
		else
		{
			SetThingVel(victim, '0 0 0');
		}
		
		count = count + 1;
		if(count >= 5)
		{
			if(GetThingType(victim) == 10)      // OTHER PLAYER
			{
				// everything is done in the victim's KYLE.COG handler
				if(!(GetThingFlags(victim) & 0x200))
				{
					SkillTarget(victim, player, 31, rank);
				}
			}
			else                                // ENEMY
			{
				DamageThing(victim, damage * rank / 2, 0x8, player);
				// Force the victim to stay in place
				SetActorFlags(victim, 0x40000);
			}
			count = 0;
			PlaySoundThing(gripSound, victim, 1.0, -1, -1, 0x80);
		}	
	}	
	Return;
# .......................................................................
timer:
	Return;
# .......................................................................
selected:
	jkPrintUNIString(player, 31);
	Return;
# .......................................................................
deselected:
	call stop_power;
	Return;
# .......................................................................
killed:
newplayer:
	if(GetSenderRef() != player) Return;
	call stop_power;
	Return;
# .......................................................................
stop_power:
#SetCameraFocus(0, player);
	if(gripTrack != -1) StopKey(player, gripTrack, 0.05);
	gripTrack = -1;
	if(chokeTrack != -1) StopKey(victim, chokeTrack, 0.3);
	chokeTrack = -1;
	active = 0;
	SetPulse(0);
	SetInvActivated(player, 31, 0);
	if(victim != -1)
	{
		#printint(old_flags);
		SetPhysicsFlags(victim, old_flags);
		ClearActorFlags(victim, 0x40000);
	}
	victim = -1;
	Return;
# .......................................................................
end
