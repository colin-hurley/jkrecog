# Jedi Knight Cog Script
#
# WEAP_BRYAR.COG
#
# WEAPON 2 Script - Bryar Pistol
#
# While the pistol retains its primary fire mode, the secondary fire features a charge-up
#  mode for a more powerful shot.  Hold the fire button longer to increase the power.
#
# Programming notes:
# -The int (boolean) variable "charging" is used to determine if the fire2 button has been
#  pressed.  For example, if we detonate the sequencers with fire 2 in the sequencer cog,
#  the deactivated message is called in the cog for the weapon that is mounted.
#  This variable guarantees that the fire2 charge code in this cog will not be executed
#  unless fire2 was actually activated in this cog, and not somewhere else. [8/25]
#
# [darthslaw]
#
# (C) 2006 JK ReCog


symbols

model       povModel=bryv.3do                   local
model       weaponMesh=bryg.3do                 local

keyframe    mountAnim=bryvmnt.key               local
keyframe    dismountAnim=bryvdis.key            local
keyframe    povfireAnim=bryvpst1.key            local
keyframe    holsterAnim=kyhlstr.key             local

sound       outSound=pistout1.wav               local
sound       mountSound=df_bry_ready.wav         local
sound       dismountSound=PutWeaponAway01.wav   local
sound       fireSound=pistol-1.wav              local

template    projectile=+bryarbolt               local

thing       player                              local

flex        fireWait=0.5                        local
flex        holsterWait                         local
flex        powerBoost                          local
flex        autoAimFOV=30                       local

int         trackID=-1                          local
int         dummy=0                             local
int         mode                                local
int         holsterTrack                        local

int			channel=-1							local
int			inv									local
flex		delayTime							local
sound       chargeSound=BCChargeUp.wav          local
int			charging							local

message		startup
message		killed
message		newplayer
message     activated
message     deactivated
message     selected
message     deselected
message     autoselect
message     fire
message     timer

end

# ========================================================================================

code
startup:
	channel = -1;
	charging = 0;
	return;

newplayer:
killed:
	charging = 0;
   if (player == GetSenderRef())
   {
      if(channel != -1)
      {
         StopSound(channel, 0.1);
         channel = -1;
      }
   }
   Return;
   
fire:
   player = GetSourceRef();

   // Check that the player is still alive.
   if(GetThingHealth(player) <= 0)
   {
      Return;
   }

   // Check Ammo - If we are out, autoselect best weapon.
   if(GetInv(player, 11) < 1.0)
   {
      PlaySoundThing(outSound, player, 1.0, -1.0, -1.0, 0x80);
      if(GetAutoSwitch() & 1)
         SelectWeapon(player, AutoSelectWeapon(player, 1));
      Return;
   }

   SetPOVShake('0.0 -.003 0.0', '1.0 0.0 0.0', .05, 80.0);
   dummy = FireProjectile(player, projectile, fireSound, 8, '0.0135 0.1624 0.0', '0 0 0', 1.0, 0x20, autoAimFOV, autoAimFOV*2);
   ChangeInv(player, 11, -1.0);
   jkPlayPOVKey(player, povfireAnim, 1, 0x38);

   powerBoost = GetInv(player, 63);
   ChangeFireRate(player, fireWait/powerBoost);

   Return;

# ........................................................................................

activated:
   player = GetSourceRef();
   mode = GetSenderRef();

   if(GetInv(player, 11) < 1.0)
   {
      PlaySoundThing(outSound, player, 1.0, -1.0, -1.0, 0x80);
      if(GetAutoSwitch() & 1)
         SelectWeapon(player, AutoSelectWeapon(player, 1));
      Return;
   }
   jkSetWaggle(player, '0.0 0.0 0.0', 0);
	if(mode == 0)
	{
		powerBoost = GetInv(player, 63);
		ActivateWeapon( player, fireWait/powerBoost, mode );
	}
	else
	{
		charging = 1;
		ActivateWeapon(player, 0, mode);
		if(channel == -1) channel = PlaySoundThing(chargeSound, player, 0.0, -1, -1, 0x181);
		if(channel != -1)
		{
			ChangeSoundPitch(channel, 0.3, 0.01);
			SetTimerEx(0.2, 10, channel, 1);
		}
	}
   Return;

# ........................................................................................

deactivated:
   player = GetSourceRef();
   mode = GetSenderRef();

   jkSetWaggle(player, '10.0 7.0 0.0', 350);
   delayTime = DeactivateWeapon( player, mode );
   powerBoost = GetInv(player, 63);
   SetMountWait(player, fireWait/powerBoost);
   KillTimerEx(10);
	if(channel != -1)
	{
		StopSound(channel, 0);
		channel = -1;
	}
	if(!charging) return;	//this was not the cog that was activated
	charging = 0;
	if(mode == 1)
	{
		// From Bowcaster Cog -- keeps weapon from firing if deselected while charged
		if (GetCurWeaponMode() != -1) Return;

		inv = GetInv(player, 11);
		if(delayTime > 1.5 && inv >= 4)
		{
			SetPOVShake('0.0 -.009 0.0', '4.0 0.0 0.0', .05, 80.0);
			FireProjectile(player, projectile, fireSound, 8, '0.0135 0.1624 0.0', '0 0 0', 4.0, 0x22, autoAimFOV, autoAimFOV*2);
			ChangeInv(player, 11, -4.0);
			print("fire level 4");
		}
		else if(delayTime > 1.0 && inv >= 3)
		{
			SetPOVShake('0.0 -.007 0.0', '3.0 0.0 0.0', .05, 80.0);
			FireProjectile(player, projectile, fireSound, 8, '0.0135 0.1624 0.0', '0 0 0', 3.0, 0x22, autoAimFOV, autoAimFOV*2);
			ChangeInv(player, 11, -3.0);
			print("fire level 3");
		}
		else if(delayTime > 0.5 && inv >= 2)
		{
			SetPOVShake('0.0 -.005 0.0', '2.0 0.0 0.0', .05, 80.0);
			FireProjectile(player, projectile, fireSound, 8, '0.0135 0.1624 0.0', '0 0 0', 2.0, 0x22, autoAimFOV, autoAimFOV*2);
			ChangeInv(player, 11, -2.0);
			print("fire level 2");
		}
		else //normal shot
		{
			SetPOVShake('0.0 -.003 0.0', '1.0 0.0 0.0', .05, 80.0);
			FireProjectile(player, projectile, fireSound, 8, '0.0135 0.1624 0.0', '0 0 0', 1.0, 0x20, autoAimFOV, autoAimFOV*2);
			ChangeInv(player, 11, -1.0);
			print("fire level 1");
		}
		jkPlayPOVKey(player, povfireAnim, 1, 0x38);
	}
   Return;

# ........................................................................................

selected:
   player = GetSourceRef();

   // Setup the meshes and models.
   jkSetPOVModel(player, povModel);
   jkSetWeaponMesh(player, weaponMesh);
   SetArmedMode(player, 1);
	if(channel != -1)
	{
		StopSound(channel, 0);
		channel = -1;
	}
   // Play mounting sound.
   PlayMode(player, 41);
   PlaySoundThing(mountSound, player, 1.0, -1.0, -1.0, 0x80);

   // Play the animation (NOLOOP + UNIQUE + ENDPAUSE).
   // The animation is held at the last frame after it is played.
   trackID = jkPlayPOVKey(player, mountAnim, 0, 0x14);
   jkSetWaggle(player, '10.0 7.0 0.0', 350);

   // Clear saber flags, and allow activation of the weapon
   jkClearFlags(player, 0x5);
   SetCurWeapon(player, 2);
   SetMountWait(player, GetKeyLen(mountAnim));

   Return;

# ........................................................................................

deselected:
   player = GetSourceRef();
   
   PlaySoundThing(dismountSound, player, 1.0, -1, -1, 0x80);
   jkPlayPOVKey(player, dismountAnim, 0, 0x18);
   
   holsterWait = GetKeyLen(holsterAnim);
   SetMountWait(player, holsterWait);
   holsterTrack = PlayKey(player, holsterAnim, 1, 0x4);
   SetTimerEx(holsterWait, 2, 0.0, 0.0);
   if (trackID != -1)
   {
      jkStopPOVKey(player, trackID, 0);
      trackID = -1;
   }
   jkSetWaggle(player, '0.0 0.0 0.0', 0);
	KillTimerEx(10);
	if(channel != -1)
	{
		StopSound(channel, 0);
		channel = -1;
	}

   Return;

# ........................................................................................

autoselect:
   player = GetSourceRef();

   // If the player has the weapon
   if(GetInv(player, 2) != 0.0)
   {
      // If the player has ammo
      if(GetInv(player, 11) != 0.0)
      {
         ReturnEx(500.0);
      }
      else
      {
         ReturnEx(-1.0);
      }
   }
   else
   {
      ReturnEx(-1.0);
   }

   Return;

# ........................................................................................

timer:
	if(GetSenderID() == 2)
	{
		StopKey(player, holsterTrack, 0.0);
	}
	else if(GetSenderID() == 10)
	{
		if(GetParam(1) == 1)
		{
			ChangeSoundVol(GetParam(0), 1.0, 0.6);
			//different pitches based on how much ammo we have
			inv = GetInv(player, 11);
			if(inv >= 4) ChangeSoundPitch(GetParam(0), 1.0, 1.6);
			else if(inv >= 3) ChangeSoundPitch(GetParam(0), 0.9, 1.2);
			else if(inv >= 2) ChangeSoundPitch(GetParam(0), 0.8, 0.8);
			else ChangeSoundPitch(GetParam(0), 0.7, 0.4);
		}
	}
	Return;

end





