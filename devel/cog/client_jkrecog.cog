# Jedi Knight Cog Script
#
# CLIENT_JKRECOG.COG
#
# The main client cog. Handles weapon firing and such
#
# [darthslaw]
#
# (c) 2007 JK ReCog

flags=0x240

symbols

message		startup
message		trigger

# Bryar Templates
template	tpl2_0=+bryarbolt		local
template	tpl2_1=+bryarsmack		local

# Stormtrooper Rifle Templates
template	tpl3_0=+stlaser			local
template	tpl3_2=+stscopelaser	local

# Thermal Detonator Templates
template	tpl4_2=+grenade_exp		local
template	tpl4_2b=+TD_Expl_Sphere	local

# Bowcaster Templates
template	tpl5_0=+crossbowbolt			local
template	tpl5_1=+expl_crossbowbolt		local
template	tpl5_0a=+crossbowbolt2			local
template	tpl5_1a=+expl_crossbowbolt2		local
template	tpl5_2=+bcscopebolt				local

# Repeater Templates

# Rail Detonator Templates
template	tpl7_0=+raildet				local
template	tpl7_1=+raildet2			local
keyframe    tpl7_1_Anim=rld0anim.key	local

# Explosion Templates
template	expl4a=+grenade_exp		local
template	expl4b=+TD_Expl_Sphere	local
template	expl7=+raildet_exp		local
template	expl8=+sequencer_exp	local


# General Variables
thing		player					local
thing		dummy=-1				local
vector		fire_offset				local

int			garbage					local
int			isJKUP					local

end

# ========================================================================================

code

startup:
	// this code courtesy of ZeqMacaw =)
	garbage = -1;
	garbage = floor(4.2);
	if(garbage == 4) isJKUP = 1;
	else isJKUP = 0;
	return;

trigger:
	player = GetSenderRef();
	if(GetSourceRef() == 2000)		// bryar modes 0 and 1
	{
		# Param 0 = X error
		# Param 1 = Y error
		# Param 2 = Damage scale
		fire_offset = '0.0135 0.1624 0.0';
		FireProjectile(player, tpl2_0, -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1), 0), GetParam(2), 0x62, 30, 60);
		return;
	}
	if(GetSourceRef() == 2001)		// bryar mode 2
	{
		# No Params
		fire_offset = '0 0.03 0.025';
		FireProjectile(player, tpl2_1, -1, -1, fire_offset, '0 0 0', 0, 0, 0, 0);
		return;
	}
	if(GetSourceRef() == 3000)		// stormtrooper rifle modes 0 and 1
	{
		# Param 0 = X error
		# Param 1 = Y error
		fire_offset = '0.0168 0.1896 0.00';
		FireProjectile(player, tpl3_0, -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1), 0), 1.0, 0x60, 25, 50);
		return; 
	}
	if(GetSourceRef() == 3002)		// stormtrooper rifle zoomed
	{
		# Param 0 = X error
		# Param 1 = Y error
		fire_offset = '0 0 0.037';
		FireProjectile(player, tpl3_2, -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1), 0), 1.0, 0x60, 25, 50);
		return; 
	}
	if(GetSourceRef() == 4005)		// thermal det - dead man switch
	{
		# No Params
		FireProjectile(player, tpl4_2, -1, -1, '0 0 0', '0 0 0', 0, 0, 0, 0);	// explosion
		FireProjectile(player, tpl4_2b, -1, -1, '0 0 0', '0 0 0', 0, 0, 0, 0);	// sphere fx
		return;
	}
	if(GetSourceRef() == 5000)		// bowcaster modes 1 and 2
	{
		# Param 0 = X error
		# Param 1 = Y error
		# Param 2 = Fire Mode
		# Param 3 = Number of bolts
		fire_offset = '0.0207 0.0888 0.00';
		if(GetParam(3) >= 1)	// at least 1 shot
		{
			FireProjectile(player, tpl5_0[GetParam(2)], -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1), 0), 1.0, 0x60, 30, 60);
		}
		if(GetParam(3) >= 3)	// at least 3 shots
		{
			FireProjectile(player, tpl5_0a[GetParam(2)], -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1) + 6, 0), 1.0, 0x60, 30, 60);
			FireProjectile(player, tpl5_0a[GetParam(2)], -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1) - 6, 0), 1.0, 0x60, 30, 60);
		}
		if(GetParam(3) >= 5)	// at least 5 shots
		{
			FireProjectile(player, tpl5_0a[GetParam(2)], -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1) + 12, 0), 1.0, 0x60, 30, 60);
			FireProjectile(player, tpl5_0a[GetParam(2)], -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1) - 12, 0), 1.0, 0x60, 30, 60);
		}
		return;
	}
	if(GetSourceRef() == 5002)		// bowcaster zoomed
	{
		# Param 0 = X error
		# Param 1 = Y error
		fire_offset = '0 0 0.037';
		FireProjectile(player, tpl5_2, -1, -1, fire_offset, VectorSet(GetParam(0), GetParam(1), 0), 1.0, 0x60, 25, 50);
		return; 
	}
	if(GetSourceRef() == 7000)		// Rail Detonator fire 1 - impact
	{
		fire_offset = '0.0214 0.15 0.00';
		dummy = FireProjectile(player, tpl7_0, -1, -1, fire_offset, '0 0 0', 1.0, 0x60, 25, 50);
		if(dummy != -1)
		{
			SetThingRotVel(dummy, VectorSet(GetParam(0), GetParam(1), GetParam(2)));
		}
		return;
	}
	if(GetSourceRef() == 7001)		// Rail Detonator fire 2 - sticky
	{
		fire_offset = '0.0214 0.15 0.00';
		dummy = FireProjectile(player, tpl7_1, -1, -1, fire_offset, '0 0 0', 1.0, 0x60, 25, 50);
		if(dummy != -1)
		{
			SetThingRotVel(dummy, VectorSet(GetParam(0), GetParam(1), GetParam(2)));
			PlayKey(dummy, tpl7_1_Anim, 1, 20);
		}
		return;
	}
	if(GetSourceRef() == 4010)		// Thermal Det powerup exploded
	{
		# param 0-2 = xyz coords 
		# param 3 = player that damaged the powerup
		dummy = CreateThingAtPos(expl4a, GetThingSector(getLocalPlayerThing()), vectorSet(GetParam(0), GetParam(1), GetParam(2)), '0 0 0');
		CreateThingAtPos(expl4b, GetThingSector(getLocalPlayerThing()), vectorSet(GetParam(0), GetParam(1), GetParam(2)), '0 0 0');
		if(isJKUP)	// if new cog verbs are available, use them
		{
			SetThingParent(dummy, GetParam(3));
		}
		return;
	}
	if(GetSourceRef() == 7010)		// Rail charge powerup exploded
	{
		# param 0-2 = xyz coords 
		# param 3 = player that damaged the powerup
		dummy = CreateThingAtPos(expl7, GetThingSector(getLocalPlayerThing()), vectorSet(GetParam(0), GetParam(1), GetParam(2)), '0 0 0');
		if(isJKUP)	// if new cog verbs are available, use them
		{
			SetThingParent(dummy, GetParam(3));
			print("Parent:");
			printint(getParam(3));
			print("Dummy:");
			printint(dummy);
			print("Local Player:");
			printint(getLocalPlayerThing());
		}
		return;
	}
	if(GetSourceRef() == 8010)		// Sequencer charge powerup exploded
	{
		# param 0-2 = xyz coords 
		# param 3 = player that damaged the powerup
		dummy = CreateThingAtPos(expl8, GetThingSector(getLocalPlayerThing()), vectorSet(GetParam(0), GetParam(1), GetParam(2)), '0 0 0');
		if(isJKUP)	// if new cog verbs are available, use them
		{
			SetThingParent(dummy, GetParam(3));
		}
		return;
	}
	return;

end

