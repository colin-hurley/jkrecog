# Jedi Knight Cog Script
#
# CLASS_SEQUENCER.COG
#
# Class COG for the Sequencer charges
#
# Set the sequencers to proximity devices after a certain time.
#
# [YB]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved


symbols

message     created
message     timer
message		removed

thing		seq			local
thing		player		local

#flex		dot			local
int			i			local

end

# ========================================================================================

code

created:
   SetTimerEx(2.0, GetSenderRef(), GetThingSignature(GetSenderRef()), 0);

   Return;

timer:
   if(GetParam(0) == GetThingSignature(GetSenderId()))
   {
      // WF_PROXIMITY, Weapon will explode when something touches its sphere.
      SetWeaponFlags(GetSenderId(), 0x1000);

      // gives it a bigger sphere
      SetThingCollideSize(GetSenderId(), 0.3);
   }

   Return;

removed:
	seq = GetSenderRef();
	// do the light flash effects
	for(i=0;i<=GetNumPlayers();i=i+1)
	{
		player = GetPlayerThing(i);
		if(HasLOS(player, seq))
		{
			// check local players' bins for IR Goggles usage and do blinding effects
			SkillTarget(player, seq, 41, 200);
		}
	}
	return;

end


