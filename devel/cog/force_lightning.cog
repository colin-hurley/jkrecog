# Jedi Knight Cog Script
#
# FORCE_LIGHTNING.COG
#
# FORCEPOWER Script - Lightning
#  Dark Side Power
#  Bin 32
#
# Now, bolts will fan out like in JO, to hit a wider target. Bolts become more powerful
#  as more stars are put on the power.
#
# [11/12/06] - Each bolt now fires on its own interval (i.e. no two bolts fire at the
#  same time).  This greatly improves the look of the lightning.
#
# [darthslaw]
#
# (C) 2006 JK ReCog


symbols

thing       player                           local

flex        cost=40.0                        local
int         rank                             local
int         mana                             local
int         bin=32                           local

template    lightning0=+force_lightning      local
template    lightning1=+force_lightning2     local
template    lightning2=+force_lightning3     local
template    lightning3=+force_lightning4     local

sound       fireSound=ForceLitning02.wav     local
int         fireChannel=-1                   local
int         modeTrack=-1                     local

int			bolt							local
int			boltmax							local

vector		vec								local

message     startup
message     activated
message     deactivated
message     fire
message     killed
message     selected

end

# ========================================================================================

code

startup:
   player = GetLocalPlayerThing();

   Return;

# ........................................................................................

fire:
	mana = GetInv(player, 14);
	if((mana < cost) || (GetThingHealth(player) <= 0))
	{
		SetBinWait(player, bin, 2);
		call stop_power;
		return;
	}
	if(GetInv(player, 64) != 1) ChangeInv(player, 14, -cost);

	if(fireChannel == -1)
		fireChannel = PlaySoundThing(fireSound, player, 1.0, -1, -1, 0x81);
	
	if(bolt == 1) vec = VectorSet((rand() - 0.5) * 2.0, (rand() - 0.5) * 1.5, 0);
	else if(bolt == 2) vec = VectorSet((rand() - 0.5) * 5.0, 7.5 + (rand() - 0.5) * 4.5, 0);
	else if(bolt == 3) vec = VectorSet((rand() - 0.5) * 5.0, -7.5 - (rand() - 0.5) * 4.5, 0);
	else if(bolt == 4) vec = VectorSet((rand() - 0.5) * 4.0, 15 + (rand() - 0.5) * 4.5, 0);
	else if(bolt == 5) vec = VectorSet((rand() - 0.5) * 4.0, -15 - (rand() - 0.5) * 4.5, 0);
	else if(bolt == 6) vec = VectorSet((rand() - 0.5) * 3.0, 22.5 + (rand() - 0.5) * 4.5, 0);
	else if(bolt == 7) vec = VectorSet((rand() - 0.5) * 3.0, -22.5 - (rand() - 0.5) * 4.5, 0);
	else vec = '0 0 0';						//just in case... make sure the vector is defined
	FireProjectile(player, lightning0[rank - 1], -1, -1, '-0.025 0.01 0', vec, 1.0, 0x0, 0, 0);
	bolt = bolt + 1;
	if(bolt > boltmax) bolt = 1;
	return;

# ........................................................................................

activated:
   rank = GetInv(player, 32);
   if(IsInvActivated(player, bin)) return;

   boltmax = (2 * rank) - 1;			//only allow a certain number of bolts per star
   if(boltmax <= 0) return;				//someone with 0 stars shouldn't have this power...

	mana = GetInv(player, 14);
	if(mana < cost) return;				// don't activate if there is not enough mana

	vec = '0 0 0';
	bolt = 0;
	cost = 40 / boltmax;
   ActivateBin(player, 0.15 / boltmax, bin);
   SetInvActivated(player, bin, 1);
   if (modeTrack == -1)
      modeTrack = PlayMode(player, 24);
   Return;

# ........................................................................................

selected:
   jkPrintUNIString(player, 32);
   Return;

# ........................................................................................

deactivated:
	if(!IsInvActivated(player, bin)) return;
	call stop_power;
	SetBinWait(player, bin, 1.0);
	Return;

# ........................................................................................

killed:
   if(GetSenderRef() != player) Return;
   call stop_power;
   Return;

# ........................................................................................

stop_power:
   SetInvActivated(player, bin, 0);
   DeactivateBin(player, bin);
   if(fireChannel != -1)
   {
      StopSound( fireChannel, 0.1);
      fireChannel = -1;
   }

   if(modeTrack != -1)
   {
      StopKey(player,modeTrack,0.5);
      modeTrack = -1;
   }

   Return;

end


